alias functionNumber R1;

// Buffer write
if (functionNumber == 1) then
    alias blockNumber R2;
    alias offset R3;
    alias wordToWrite R4;
    alias bufferIndex R5;

    bufferIndex = blockNumber % MAX_BUFFER;

    multipush(R1, R2, R3, R4, R5); //acquire buffer
    R1 = 1;
    R2 = bufferIndex;
    R3 = [SYSTEM_STATUS_TABLE+1];
    call RESOURCE_MANAGER;
    multipop(R1, R2, R3, R4, R5);


    if ([BUFFER_TABLE + bufferIndex*4 + 0] != blockNumber) then
        if ([BUFFER_TABLE + bufferIndex*4 + 1] == 1) then
            multipush(R1, R2, R3, R4, R5); //disk store
            R1 = 1;
            R2 = [SYSTEM_STATUS_TABLE+1];
            R3 = 71 + bufferIndex;
            R4 = [BUFFER_TABLE + bufferIndex*4 + 0];
            call DEVICE_MANAGER;
            multipop(R1, R2, R3, R4, R5);

            [BUFFER_TABLE + bufferIndex*4 + 1] = 0;
        endif;
        multipush(R1, R2, R3, R4, R5); // disk load
        R1 = 2;
        R4 = blockNumber;
        R2 = [SYSTEM_STATUS_TABLE+1];
        R3 = 71 + bufferIndex;
        call DEVICE_MANAGER;
        multipop(R1, R2, R3, R4, R5);
        [BUFFER_TABLE + bufferIndex*4 + 0] = blockNumber;
    endif;
    
    [(71 + bufferIndex)*512 + offset] = wordToWrite;
    
    [BUFFER_TABLE + bufferIndex*4 + 1] = 1;

    multipush(R1, R2, R3, R4, R5);
    R1 = 2;
    R2 = bufferIndex;
    R3 = [SYSTEM_STATUS_TABLE+1];  
    call RESOURCE_MANAGER;
    multipop(R1, R2, R3, R4, R5);
    
    return;
endif;

// Buffer read
if (functionNumber == 2) then
    alias blockNumber R2;
    alias offset R3;
    alias memAddress R4;
    alias bufferIndex R5;
    bufferIndex = blockNumber % MAX_BUFFER;
    multipush(R1, R2, R3, R4, R5);
    R1 = 1;
    R2 = bufferIndex;
    R3 = [SYSTEM_STATUS_TABLE+1];
    call RESOURCE_MANAGER;
    multipop(R1, R2, R3, R4, R5);

    alias BUFFER_TABLEEntry R6;
    BUFFER_TABLEEntry = BUFFER_TABLE + bufferIndex*4;

    if ([BUFFER_TABLEEntry + 0] != blockNumber) then
        if ([BUFFER_TABLEEntry + 1] == 1) then
            multipush(R1, R2, R3, R4, R5, R6);
            R1 = 1;
            R2 = [SYSTEM_STATUS_TABLE+1];
            R3 = 71 + bufferIndex;
            R4 = [BUFFER_TABLEEntry + 0];
            call DEVICE_MANAGER;
            multipop(R1, R2, R3, R4, R5, R6);

            [BUFFER_TABLEEntry + 1] = 0;
        endif;
        multipush(R1, R2, R3, R4, R5, R6);
        R1 = 2;
        R4 = blockNumber;
        R2 = [SYSTEM_STATUS_TABLE+1];
        R3 = 71 + bufferIndex;
        call DEVICE_MANAGER;
        multipop(R1, R2, R3, R4, R5, R6);

        [BUFFER_TABLEEntry + 0] = blockNumber;
    endif;

    [memAddress] = [(71 + bufferIndex)*512 + offset];
    multipush(R1, R2, R3, R4, R5, R6);
    R1 = 2;
    R2 = bufferIndex;
    R3 = [SYSTEM_STATUS_TABLE+1];  
    call RESOURCE_MANAGER;
    multipop(R1, R2, R3, R4, R5, R6);
endif;

if (functionNumber == 3) then // open function
    alias fileName R2;
    alias i R3;
    i = 0;
    while (i < MAX_FILE_NUM && [INODE_TABLE + i * 16 + 1] != fileName) do
        i = i + 1;
    endwhile;
    if (i == MAX_FILE_NUM) then
        R0 = -1;
    else
        alias returnValue R4;
        multipush(R0, R1, R2, R3);
        R1 = 4;
        R2 = i;
        R3 = [SYSTEM_STATUS_TABLE + 1];
        call RESOURCE_MANAGER; // acquire inode

        returnValue = R0;
        multipop(R0, R1, R2, R3);

        if (returnValue == -1) then
            R0 = -1;
        else
            if ([INODE_TABLE + i * 16 + 0] == EXEC) then // release inode
                multipush(R0, R1, R2, R3, R4);
                R1 = 5;
                R2 = i;
                R3 = [SYSTEM_STATUS_TABLE + 1];
                call RESOURCE_MANAGER;
                multipop(R0, R1, R2, R3, R4);
                R0 = -1;
            else
                alias openFileTableEntry R5;
                openFileTableEntry = 0;
                while (openFileTableEntry < MAX_OPENFILE_NUM) do
                    if ([OPEN_FILE_TABLE + openFileTableEntry * 4] == -1) then
                        break;
                    endif;
                    openFileTableEntry = openFileTableEntry + 1;
                endwhile;

                if (openFileTableEntry == MAX_OPENFILE_NUM) then
                    R0 = -2;
                else
                    if (fileName == "root") then
                        [OPEN_FILE_TABLE + openFileTableEntry * 4] = INODE_ROOT;
                    else
                        if ([FILE_STATUS_TABLE + i * 4 + 1] == -1) then
                            [FILE_STATUS_TABLE + i * 4 + 1] = 1;
                        else
                            [FILE_STATUS_TABLE + i * 4 + 1] = [FILE_STATUS_TABLE + i * 4 + 1] + 1;
                        endif;
                        [OPEN_FILE_TABLE + openFileTableEntry * 4 + 0] = i;
                    endif;
                    [OPEN_FILE_TABLE + openFileTableEntry * 4 + 1] = 1;
                    [OPEN_FILE_TABLE + openFileTableEntry * 4 + 2] = 0;
                    multipush(R0, R1, R2, R3, R4, R5);
                    R1 = 5;
                    R2 = i;
                    R3 = [SYSTEM_STATUS_TABLE + 1];
                    call RESOURCE_MANAGER; // relase inode
                    multipop(R0, R1, R2, R3, R4, R5);

                    R0 = openFileTableEntry;
                endif;
            endif;
        endif;
    endif;
    return;
endif;

if (functionNumber == 4) then // close function
    alias openFileTableEntry R2;
    alias inodeIndex R3;
    inodeIndex = [OPEN_FILE_TABLE + openFileTableEntry * 4];
    [OPEN_FILE_TABLE + openFileTableEntry * 4 + 1] = [OPEN_FILE_TABLE + openFileTableEntry * 4 + 1] - 1;

    if ([OPEN_FILE_TABLE + openFileTableEntry * 4 + 1] == 0) then
        alias i R4;
        i = 0;
        while (i < 4) do
            [OPEN_FILE_TABLE + openFileTableEntry * 4 + i] = -1;
            i = i + 1;
        endwhile;

        if (inodeIndex != INODE_ROOT) then
            [FILE_STATUS_TABLE + inodeIndex * 4 + 1] = [FILE_STATUS_TABLE + inodeIndex * 4 + 1] - 1;
            if ([FILE_STATUS_TABLE + inodeIndex * 4 + 1] == 0) then
                [FILE_STATUS_TABLE + inodeIndex * 4 + 1] = -1;
            endif;
        endif;
    endif;
endif;

return;