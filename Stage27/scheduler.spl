alias currentPID R0;
alias process_table_entry R1;

currentPID = [SYSTEM_STATUS_TABLE + 1];
process_table_entry = PROCESS_TABLE + 16 * currentPID;

multipush(BP);

[process_table_entry + 12] = SP % 512;
[process_table_entry + 14] = PTBR;
[process_table_entry + 15] = PTLR;

alias i R2;
alias state R3;
alias newPID R4;

if ([SYSTEM_STATUS_TABLE + 5] != 0) then
    if ([PROCESS_TABLE + 15 * 16 + 4] == WAIT_DISK) then
        i = 0;
    else
        i = SWAPPER_DAEMON;
    endif;
else
    i = currentPID + 1;
    while (i < 16) do
        state = [PROCESS_TABLE + i * 16 + 4];
        if ((state == READY || state == CREATED) && [PROCESS_TABLE + i * 16 + 6] != 1) then
            break;
        endif;
        i = i + 1;
    endwhile;

    if (i == 16) then
        i = 1;
        while (i < currentPID) do
            state = [PROCESS_TABLE + i * 16 + 4];
            if ((state == CREATED || state == READY) && [PROCESS_TABLE + i * 16 + 6] != 1) then
                break;
            endif;
            i = i + 1;
        endwhile;
    endif;

    if (i == currentPID) then
        i = 0;
    endif;
endif;

newPID = i;

process_table_entry = PROCESS_TABLE + newPID * 16;
SP = [process_table_entry + 11] * 512 + [process_table_entry + 12];
PTBR = [process_table_entry + 14];
PTLR = [process_table_entry + 15];

[SYSTEM_STATUS_TABLE + 1] = newPID;

if ([process_table_entry + 4] == CREATED) then
    SP = [process_table_entry + 13];
    BP = [[process_table_entry + 11] * 512];
    [process_table_entry + 4] = RUNNING;
    [process_table_entry + 9] = 0; // mode flag
    ireturn;
endif;

[process_table_entry + 4] = RUNNING;

multipop(BP);

return;